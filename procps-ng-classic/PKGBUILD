# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

_pkgname=procps
pkgname=procps-ng-classic
pkgver=3.3.15
pkgrel=1
commit=7bb949bcba13c107fa0f45d2d0298b1ad6b6d6cc
pkgdesc='Utilities for monitoring your system and its processes (with classic top)'
url='https://gitlab.com/procps-ng/'
license=('GPL' 'LGPL')
arch=('i686' 'x86_64')
depends=('ncurses' 'systemd')
source=("https://gitlab.com/${_pkgname}-ng/${_pkgname}/-/archive/v${pkgver}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('440af4845fee9f55b721aa456f88259668465ddb37168ea1e2d2ef195f55bbc4')

groups=('base')

conflicts=('procps' 'sysvinit-tools' 'procps-ng')
provides=('procps' 'sysvinit-tools' 'procps-ng')
replaces=('procps' 'sysvinit-tools')

install=install

prepare() {
  cd "${srcdir}/${_pkgname}-v${pkgver}-${commit}"
  sed 's:<ncursesw/:<:g' -i watch.c
}

build() {
  cd "${srcdir}/${_pkgname}-v${pkgver}-${commit}"
  ./autogen.sh

  ./configure \
    --prefix=/usr \
    --exec-prefix=/ \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --enable-watch8bit \
    --with-systemd \
    --disable-modern-top \

  make
}

package() {
  cd "${srcdir}/${_pkgname}-v${pkgver}-${commit}"
  make DESTDIR="${pkgdir}" install

  # provided by util-linux
  rm "${pkgdir}/usr/bin/kill"
  rm "${pkgdir}/usr/share/man/man1/kill.1"
}
