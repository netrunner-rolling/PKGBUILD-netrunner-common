# $Id$
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=calamares-netrunner
pkgver=3.1.0
_pkgver=3.1
pkgrel=1
_commit=3c2275f0d7d016e92ad197bc1748bb311496c484
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/calamares"
license=('LGPL')
groups=('netrunner-common')
conflicts=('calamares')
provides=('calamares')
depends=('kconfig' 'kcoreaddons' 'ki18n' 'solid' 'yaml-cpp' 'kpmcore>=3.0.2' 'mkinitcpio-openswap'
         'boost-libs' 'hwinfo' 'qt5-svg' 'polkit-qt5' 'gtk-update-icon-cache' 'pythonqt')
makedepends=('extra-cmake-modules' 'qt5-tools' 'git' 'boost')
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source+=(#"$pkgname-$pkgver.tar.gz::https://github.com/manjaro/calamares/archive/v${_pkgver}.tar.gz"
         "$pkgname-$pkgver-$pkgrel.tar.gz::https://github.com/manjaro/calamares/archive/${_commit}.tar.gz"
         'git+https://github.com/netrunner-rolling/calamares-netrunner')

sha256sums=('0f0655224aaefb81e8aeba23cc63506899d8db442a8ef6640cf4a25293385006'
            'SKIP')


prepare() {
	mv ${srcdir}/calamares-${_commit} ${srcdir}/calamares-${_pkgver}
	cd ${srcdir}/calamares-${_pkgver}
	cp -a "${srcdir}/calamares-netrunner/." "${srcdir}/calamares-${_pkgver}"
	rm -rf "$srcdir/calamares-${_pkgver}/.git"
    # patches here
}

build() {
	cd ${srcdir}/calamares-${_pkgver}
	
	mkdir -p build
	cd build
    cmake .. \
          -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=lib \
          -DWITH_PYTHONQT:BOOL=ON \
          -DSKIP_MODULES="webview interactiveterminal initramfs \
                          initramfscfg dracut dracutlukscfg \
                          dummyprocess dummypython dummycpp \
                          dummypythonqt"
    make
}

package() {
	cd ${srcdir}/calamares-${_pkgver}/build
	make DESTDIR="$pkgdir" install
	install -Dm644 "../data/calamares-netrunner.png" "$pkgdir/usr/share/icons/hicolor/128x128/apps/calamares-netrunner.png"
	install -Dm644 "../data/calamares.desktop" "$pkgdir/usr/share/applications/calamares.desktop"
	install -Dm755 "../data/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	install -Dm644 "../data/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
	chmod 750      "$pkgdir"/etc/polkit-1/rules.d
	rm -r "$pkgdir/usr/share/icons/hicolor/scalable"
}
